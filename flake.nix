{
  description =
    "Cardano transaction tooling (builder, structural diff, blueprint)";
  nixConfig = {
    extra-substituters = [ "https://cache.iog.io" ];
    extra-trusted-public-keys =
      [ "hydra.iohk.io:f/Ea+s+dFdN+3Y/G+FDgSq+a5NEWhJGzdjvKNGv0/EQ=" ];
  };
  inputs = {
    haskellNix = {
      url =
        "github:input-output-hk/haskell.nix/8b447d7f57d62fab9249f79bb916bc891e29b9d0";
      inputs.hackage.follows = "hackageNix";
    };
    hackageNix = {
      url = "github:input-output-hk/hackage.nix/b6b4aa4bd699f743238da45c7f43da5a26a822f7";
      flake = false;
    };
    nixpkgs.follows = "haskellNix/nixpkgs-unstable";
    lintNixpkgs.url =
      "github:NixOS/nixpkgs/647e5c14cbd5067f44ac86b74f014962df460840";
    flake-parts.url = "github:hercules-ci/flake-parts";
    bundlers = {
      url = "github:NixOS/bundlers";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    dev-assets.url = "github:paolino/dev-assets";
    iohkNix = {
      url =
        "github:input-output-hk/iohk-nix/f444d972c301ddd9f23eac4325ffcc8b5766eee9";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    CHaP = {
      url =
        "github:intersectmbo/cardano-haskell-packages/887d73ce434831e3a67df48e070f4f979b3ac5a6";
      flake = false;
    };
    mkdocs.url = "github:paolino/dev-assets?dir=mkdocs";
    cardano-node = {
      url = "github:IntersectMBO/cardano-node/10.7.0";
    };
    cardano-node-clients = {
      url =
        "github:lambdasistemi/cardano-node-clients/ca86f11d27b34e37d3814e4d3c3d66e256400403";
      flake = false;
    };
  };

  outputs = inputs@{ self, nixpkgs, lintNixpkgs, flake-parts, haskellNix
    , hackageNix, iohkNix, CHaP, mkdocs, cardano-node, cardano-node-clients
    , ... }:
    let
      imageTag =
        self.shortRev or (self.dirtyShortRev or "dirty");
    in
    flake-parts.lib.mkFlake { inherit inputs; } {
      systems = [ "x86_64-linux" "aarch64-darwin" ];
      flake = {
        inherit imageTag;
      };
      perSystem = { system, ... }:
        let
          pkgs = import nixpkgs {
            overlays = [
              iohkNix.overlays.crypto
              haskellNix.overlay
              iohkNix.overlays.haskell-nix-crypto
              iohkNix.overlays.cardano-lib
            ];
            inherit system;
          };
          lib = pkgs.lib;
          lintPkgs = import lintNixpkgs { inherit system; };
          indexState = "2026-02-17T10:15:41Z";
          indexTool = { index-state = indexState; };
          fix-libs = { lib, pkgs, ... }: {
            packages.cardano-crypto-praos.components.library.pkgconfig =
              lib.mkForce [ [ pkgs.libsodium-vrf ] ];
            packages.cardano-crypto-class.components.library.pkgconfig =
              lib.mkForce
                [ [ pkgs.libsodium-vrf pkgs.secp256k1 pkgs.libblst ] ];
            packages.cardano-lmdb.components.library.pkgconfig =
              lib.mkForce [ [ pkgs.lmdb ] ];
            packages.cardano-ledger-binary.components.library.doHaddock =
              lib.mkForce false;
            packages.plutus-core.components.library.doHaddock =
              lib.mkForce false;
            packages.plutus-ledger-api.components.library.doHaddock =
              lib.mkForce false;
            packages.plutus-tx.components.library.doHaddock =
              lib.mkForce false;
          } // lib.optionalAttrs (system == "x86_64-linux") {
            packages.blockio-uring.components.library.pkgconfig =
              lib.mkForce [ [ pkgs.liburing ] ];
          };
          project = pkgs.haskell-nix.cabalProject' {
            name = "cardano-tx-tools";
            src = ./.;
            compiler-nix-name = "ghc9123";
            shell = {
              withHoogle = true;
              tools = {
                cabal = indexTool;
              };
              buildInputs = [
                lintPkgs.haskellPackages.cabal-fmt
                lintPkgs.haskellPackages.fourmolu
                lintPkgs.haskellPackages.hlint
                pkgs.just
                pkgs.curl
                pkgs.cacert
                pkgs.lmdb
                pkgs.liburing
                mkdocs.packages.${system}.from-nixpkgs
              ];
              shellHook = ''
                export SSL_CERT_FILE=${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt
              '';
            };
            modules = [ fix-libs ];
            inputMap = {
              "https://chap.intersectmbo.org/" = CHaP;
            };
          };
          components = project.hsPkgs.cardano-tx-tools.components;
          # tx-diff's web2 resolver uses http-client-tls and needs a CA
          # bundle at runtime. Wrap the raw executable so SSL_CERT_FILE
          # defaults to the bundled cacert; users can still override.
          # AppImage / DEB / RPM bundlers carry the wrapper's full nix
          # closure (including nss-cacert), so HTTPS works post-install.
          txDiff = pkgs.symlinkJoin {
            name = "tx-diff";
            paths = [ components.exes.tx-diff ];
            nativeBuildInputs = [ pkgs.makeWrapper ];
            postBuild = ''
              wrapProgram $out/bin/tx-diff \
                --set-default SSL_CERT_FILE \
                  ${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt
            '';
          };
          packageVersion =
            let
              versionLines =
                builtins.filter (lib.hasPrefix "version:")
                  (lib.splitString "\n"
                    (builtins.readFile ./cardano-tx-tools.cabal));
            in
            builtins.elemAt
              (builtins.match
                "version:[[:space:]]*([^[:space:]]+)"
                (builtins.head versionLines))
              0;
          sourceRevision =
            self.shortRev or (self.dirtyShortRev or "dirty");
          devArtifactVersion = "${packageVersion}-${sourceRevision}";
          mkDarwinHomebrewBundle =
            inputs.dev-assets.lib.mkDarwinHomebrewBundle { inherit pkgs; };
          txDiffDarwinFormulaTest = ''
            output = shell_output("#{bin}/tx-diff 2>&1", 1)
            assert_match "Usage:", output
          '';
          mkTxDiffDarwinHomebrewBundle = args:
            mkDarwinHomebrewBundle ({
              pname = "tx-diff";
              version = packageVersion;
              owner = "lambdasistemi";
              repo = "cardano-tx-tools";
              desc =
                "Compare Conway transactions with blueprint-aware data diffs";
              formulaClass = "TxDiff";
              executables = {
                tx-diff = txDiff;
              };
              executableNames = [ "tx-diff" ];
              formulaTest = txDiffDarwinFormulaTest;
              smokeCommands = [
                ''
                  set +e
                  tx-diff >/tmp/tx-diff.out 2>&1
                  status="$?"
                  set -e
                  test "$status" -ne 0
                  grep -F "Usage:" /tmp/tx-diff.out >/dev/null
                  grep -F "[--blueprint FILE ...]" /tmp/tx-diff.out >/dev/null
                ''
              ];
            } // args);
          darwinReleasePackages = lib.optionalAttrs
            pkgs.stdenv.isDarwin
            {
              darwin-release-artifacts =
                mkTxDiffDarwinHomebrewBundle { };
              darwin-dev-homebrew-artifacts =
                mkTxDiffDarwinHomebrewBundle {
                  artifactVersion = devArtifactVersion;
                  releaseTag = "dev-homebrew";
                  formulaName = "tx-diff-dev";
                  formulaClass = "TxDiffDev";
                  formulaVersion = devArtifactVersion;
                };
            };
          linuxReleasePackages = lib.optionalAttrs
            pkgs.stdenv.isLinux
            {
              linux-release-artifacts =
                import ./nix/linux-release.nix {
                  inherit pkgs system packageVersion;
                  executableName = "tx-diff";
                  package = txDiff;
                  bundlers = inputs.bundlers;
                };
              linux-dev-release-artifacts =
                import ./nix/linux-release.nix {
                  inherit pkgs system packageVersion;
                  artifactVersion = devArtifactVersion;
                  executableName = "tx-diff";
                  package = txDiff;
                  bundlers = inputs.bundlers;
                };
              cardano-tx-generator-linux-release-artifacts =
                import ./nix/linux-release.nix {
                  inherit pkgs system packageVersion;
                  executableName = "cardano-tx-generator";
                  package = components.exes.cardano-tx-generator;
                  bundlers = inputs.bundlers;
                };
              cardano-tx-generator-linux-dev-release-artifacts =
                import ./nix/linux-release.nix {
                  inherit pkgs system packageVersion;
                  artifactVersion = devArtifactVersion;
                  executableName = "cardano-tx-generator";
                  package = components.exes.cardano-tx-generator;
                  bundlers = inputs.bundlers;
                };
              linux-artifact-smoke =
                import ./nix/linux-artifact-smoke.nix {
                  inherit pkgs system;
                };
            };
          cardanoTxGeneratorImage =
            import ./nix/docker-image.nix {
              inherit pkgs components imageTag;
            };
          checkSuite = import ./nix/checks.nix {
            inherit pkgs components lintPkgs;
            src = ./.;
            cardanoNode =
              cardano-node.packages.${system}.cardano-node;
            cardanoNodeClientsSrc = cardano-node-clients;
          };
          checkApps = import ./nix/apps.nix {
            inherit pkgs;
            inherit (checkSuite) scripts;
          };
        in {
          packages = {
            default = txDiff;
            tx-diff = txDiff;
            cardano-tx-generator =
              components.exes.cardano-tx-generator;
          } // darwinReleasePackages // linuxReleasePackages
            // lib.optionalAttrs pkgs.stdenv.isLinux {
              cardano-tx-generator-image = cardanoTxGeneratorImage;
            };
          checks = checkSuite.checks;
          apps = checkApps // {
            tx-diff = {
              type = "app";
              program = "${txDiff}/bin/tx-diff";
            };
            cardano-tx-generator = {
              type = "app";
              program = "${
                  components.exes.cardano-tx-generator
                }/bin/cardano-tx-generator";
            };
          } // lib.optionalAttrs pkgs.stdenv.isLinux {
            linux-artifact-smoke = {
              type = "app";
              program =
                "${linuxReleasePackages.linux-artifact-smoke}/bin/linux-artifact-smoke";
            };
          };
          devShells.default = project.shell;
        };
    };
}
