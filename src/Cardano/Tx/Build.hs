{-# LANGUAGE DataKinds #-}
{-# LANGUAGE ExistentialQuantification #-}
{-# LANGUAGE GADTs #-}
{-# LANGUAGE RankNTypes #-}

{- |
Module      : Cardano.Tx.Build
Description : Operational monad transaction builder DSL
License     : Apache-2.0

Monadic transaction builder for Conway-era Cardano
transactions. Instructions are a GADT interpreted
by 'draft' and 'build'. Three non-building
instructions: 'Peek' (fixpoint values from the
assembled Tx), 'Valid' (opt-in validation checks),
and 'Ctx' (pluggable domain queries).

Parameterized by @q@ (query GADT for domain
context) and @e@ (custom validation error type).
-}
module Cardano.Tx.Build (
    -- * Monad
    TxBuild,
    TxInstr (..),

    -- * Convergence
    Convergence (..),
    Check (..),
    LedgerCheck (..),

    -- * Interpreters
    Interpret (..),
    InterpretIO (..),

    -- * Witnesses
    SpendWitness (..),
    MintWitness (..),
    WithdrawWitness (..),
    CertWitness (..),
    ProposalWitness (..),

    -- * Input combinators
    spend,
    spendScript,
    reference,
    collateral,

    -- * Output combinators
    payTo,
    payTo',
    output,

    -- * Minting
    mint,

    -- * Withdrawals and metadata
    withdraw,
    withdrawScript,
    setMetadata,

    -- * Certificates
    certify,
    registerAndVoteAbstain,

    -- * Proposals
    propose,
    proposeTreasuryWithdrawal,

    -- * Votes
    vote,

    -- * Constraints
    validFrom,
    validTo,
    setCollateralReturn,
    requireSignature,
    attachScript,

    -- * Deferred
    peek,
    valid,
    ctx,

    -- * Checkers
    checkMinUtxo,
    checkTxSize,

    -- * Assembly
    draft,
    draftWith,
    build,
    buildWith,
    BuildOptions (..),
    defaultBuildOptions,

    -- * Errors
    BuildError (..),

    -- * Conway ledger types
    ConwayEra,
    AccountAddress (..),
    AccountId (..),
    pattern RewardAccount,
    Anchor (..),
    Coin (..),
    ConwayDelegCert (..),
    ConwayGovCert (..),
    ConwayTxCert (..),
    Credential (..),
    Delegatee (..),
    DRep (..),
    GovAction (..),
    GovActionId (..),
    GovActionIx (..),
    KeyRole (..),
    ProposalProcedure (..),
    ScriptHash (..),
    StrictMaybe (..),
    Vote (..),
    Voter (..),
    VotingProcedure (..),
    VotingProcedures (..),

    -- * Internal (for testing)
    interpretWith,
    assembleTx,
    bumpFee,
) where

import Cardano.Binary (serialize')
import Control.Monad.Operational (
    Program,
    ProgramViewT (Return, (:>>=)),
    singleton,
    view,
 )
import Data.ByteString qualified as BS
import Data.Foldable (toList)
import Data.Functor.Identity (
    runIdentity,
 )
import Data.List (elemIndex)
import Data.Map.Strict (Map)
import Data.Map.Strict qualified as Map
import Data.OSet.Strict qualified as OSet
import Data.Sequence.Strict qualified as StrictSeq
import Data.Set (Set)
import Data.Set qualified as Set
import Data.Word (Word32, Word64)
import Numeric.Natural (Natural)

import Cardano.Ledger.Address (
    AccountAddress (..),
    AccountId (..),
    Addr,
    Withdrawals (..),
    pattern RewardAccount,
 )
import Cardano.Ledger.Allegra.Scripts (ValidityInterval (..))
import Cardano.Ledger.Alonzo.Scripts (AsIx (..))
import Cardano.Ledger.Alonzo.TxBody (
    scriptIntegrityHashTxBodyL,
 )
import Cardano.Ledger.Alonzo.TxWits (
    Redeemers (..),
 )
import Cardano.Ledger.Api.PParams (ppMaxTxSizeL)
import Cardano.Ledger.Api.Scripts.Data (
    Data (..),
    Datum (..),
    dataToBinaryData,
 )
import Cardano.Ledger.Api.Tx (
    auxDataTxL,
    bodyTxL,
    estimateMinFeeTx,
    mkBasicTx,
    witsTxL,
 )
import Cardano.Ledger.Api.Tx.Body (
    certsTxBodyL,
    collateralInputsTxBodyL,
    feeTxBodyL,
    inputsTxBodyL,
    mintTxBodyL,
    mkBasicTxBody,
    outputsTxBodyL,
    proposalProceduresTxBodyL,
    referenceInputsTxBodyL,
    reqSignerHashesTxBodyL,
    vldtTxBodyL,
    votingProceduresTxBodyL,
    withdrawalsTxBodyL,
 )
import Cardano.Ledger.Api.Tx.Out (
    TxOut,
    coinTxOutL,
    datumTxOutL,
    getMinCoinTxOut,
    mkBasicTxOut,
 )
import Cardano.Ledger.Api.Tx.Wits (
    rdmrsTxWitsL,
    scriptTxWitsL,
 )
import Cardano.Ledger.BaseTypes (
    StrictMaybe (SJust, SNothing),
    strictMaybeToMaybe,
 )
import Cardano.Ledger.Coin (Coin (..))
import Cardano.Ledger.Conway (ConwayEra)
import Cardano.Ledger.Conway.Governance (
    Anchor (..),
    GovAction (..),
    GovActionId (..),
    GovActionIx (..),
    ProposalProcedure (..),
    Vote (..),
    Voter (..),
    VotingProcedure (..),
    VotingProcedures (..),
 )
import Cardano.Ledger.Conway.Scripts (
    ConwayPlutusPurpose (..),
 )
import Cardano.Ledger.Conway.TxCert (
    ConwayDelegCert (..),
    ConwayGovCert (..),
    ConwayTxCert (..),
    Delegatee (..),
 )
import Cardano.Ledger.Core (
    PParams,
    Script,
    auxDataHashTxBodyL,
    hashScript,
    hashTxAuxData,
    metadataTxAuxDataL,
    mkBasicTxAuxData,
 )
import Cardano.Ledger.Credential (Credential (..))
import Cardano.Ledger.DRep (DRep (..))
import Cardano.Ledger.Hashes (ScriptHash (..))
import Cardano.Ledger.Keys (
    KeyHash,
    KeyRole (..),
 )
import Cardano.Ledger.Mary.Value (
    AssetName,
    MaryValue,
    MultiAsset (..),
    PolicyID,
 )
import Cardano.Ledger.Metadata (Metadatum)
import Cardano.Ledger.Plutus.ExUnits (ExUnits (..))
import Cardano.Ledger.Plutus.Language (
    Language (PlutusV3),
 )
import Cardano.Ledger.TxIn (TxIn)
import Cardano.Slotting.Slot (SlotNo)
import Cardano.Tx.Balance (
    BalanceError,
    BalanceResult (..),
    CollateralUtxos (..),
    balanceTxWith,
 )
import Cardano.Tx.Inputs (spendingIndex)
import Cardano.Tx.Ledger (ConwayTx)
import Cardano.Tx.Scripts (
    computeScriptIntegrity,
    evalBudgetExUnits,
    refScriptsSize,
 )
import Lens.Micro ((&), (.~), (^.))
import PlutusCore.Data qualified as PLC
import PlutusTx.Builtins.Internal (
    BuiltinData (..),
 )
import PlutusTx.IsData.Class (ToData (..))

-- ----------------------------------------------------
-- Core types
-- ----------------------------------------------------

-- | Fixpoint convergence signal.
data Convergence a
    = -- | Not converged yet; use this value and
      --       keep iterating.
      Iterate a
    | -- | Converged; use this value and stop.
      Ok a
    deriving (Show, Eq, Functor)

-- | Validation result for the final transaction.
data Check e
    = -- | Validation passed.
      Pass
    | -- | One of the library-provided checks failed.
      LedgerFail LedgerCheck
    | -- | A user-provided check failed.
      CustomFail e
    deriving (Show, Eq)

-- | Closed set of library-provided validation failures.
data LedgerCheck
    = MinUtxoViolation Word32 Coin Coin
    | TxSizeExceeded Natural Natural
    | ValueNotConserved MaryValue MaryValue
    | CollateralInsufficient Coin Coin
    deriving (Show, Eq)

-- | How a spent input is witnessed.
data SpendWitness
    = -- | Pub-key input, no redeemer needed.
      PubKeyWitness
    | -- | Script input with a typed redeemer.
      forall r. (ToData r) => ScriptWitness r

-- | How a minting operation is witnessed.
data MintWitness
    = -- | Plutus script with a typed redeemer.
      forall r. (ToData r) => PlutusScriptWitness r

-- | How a withdrawal is witnessed.
data WithdrawWitness
    = -- | Pub-key withdrawal, no redeemer needed.
      PubKeyWithdraw
    | -- | Script withdrawal with a typed redeemer.
      forall r. (ToData r) => ScriptWithdraw r

-- | How a Conway certificate is witnessed.
data CertWitness
    = -- | Pub-key certificate, no redeemer needed.
      PubKeyCert
    | -- | Script certificate with a typed redeemer.
      forall r. (ToData r) => ScriptCert r

-- | How a Conway proposal procedure is witnessed.
data ProposalWitness
    = -- | No guardrail script, no redeemer needed.
      NoProposalScript
    | -- | Guardrail script with a typed redeemer.
      forall r. (ToData r) => GuardrailProposal r

-- | Pure query interpreter.
newtype Interpret q = Interpret
    { runInterpret :: forall x. q x -> x
    }

-- | Effectful query interpreter.
newtype InterpretIO q = InterpretIO
    { runInterpretIO :: forall x. q x -> IO x
    }

-- ----------------------------------------------------
-- Instruction GADT
-- ----------------------------------------------------

-- | Transaction building instructions.
data TxInstr q e a where
    -- | Spend an input with a witness.
    Spend ::
        TxIn -> SpendWitness -> TxInstr q e ()
    -- | Add a reference input.
    Reference :: TxIn -> TxInstr q e ()
    -- | Add a collateral input.
    Collateral :: TxIn -> TxInstr q e ()
    -- | Add an output.
    Send ::
        TxOut ConwayEra -> TxInstr q e ()
    -- | Mint or burn tokens.
    MintI ::
        PolicyID ->
        AssetName ->
        Integer ->
        MintWitness ->
        TxInstr q e ()
    -- | Withdraw stake rewards.
    Withdraw ::
        AccountAddress ->
        Coin ->
        WithdrawWitness ->
        TxInstr q e ()
    -- | Add a Conway certificate.
    Certify ::
        ConwayTxCert ConwayEra ->
        CertWitness ->
        TxInstr q e ()
    -- | Add a Conway proposal procedure.
    Propose ::
        ProposalProcedure ConwayEra ->
        ProposalWitness ->
        TxInstr q e ()
    -- | Add a Conway voting procedure.
    VoteI ::
        Voter ->
        GovActionId ->
        VotingProcedure ConwayEra ->
        TxInstr q e ()
    -- | Set transaction metadata for a label.
    SetMetadata ::
        Word64 ->
        Metadatum ->
        TxInstr q e ()
    -- | Require a key signature.
    ReqSignature ::
        KeyHash Guard -> TxInstr q e ()
    -- | Attach a Plutus script.
    AttachScript ::
        Script ConwayEra -> TxInstr q e ()
    -- | Set the lower validity bound.
    SetValidFrom :: SlotNo -> TxInstr q e ()
    -- | Set the upper validity bound.
    SetValidTo :: SlotNo -> TxInstr q e ()
    -- | Override the address that receives the
    --     collateral-return output. Last-write-wins.
    SetCollReturn :: Addr -> TxInstr q e ()
    -- | Peek at the final Tx (fixpoint).
    Peek ::
        (ConwayTx -> Convergence a) ->
        TxInstr q e a
    -- | Validate the final Tx after convergence.
    Valid ::
        (ConwayTx -> Check e) ->
        TxInstr q e ()
    -- | Query external context.
    Ctx :: q a -> TxInstr q e a

-- | Monadic transaction builder.
type TxBuild q e = Program (TxInstr q e)

-- ----------------------------------------------------
-- Smart constructors
-- ----------------------------------------------------

{- | Spend a pub-key UTxO. Returns the spending
index in the final sorted input set (resolved
via 'Peek').
-}
spend :: TxIn -> TxBuild q e Word32
spend txIn = do
    singleton $ Spend txIn PubKeyWitness
    singleton $ Peek $ \tx ->
        let ins = tx ^. bodyTxL . inputsTxBodyL
         in if Set.member txIn ins
                then Ok (spendingIndex txIn ins)
                else Iterate 0

{- | Spend a script UTxO with a typed redeemer.
Returns the spending index.
-}
spendScript ::
    (ToData r) => TxIn -> r -> TxBuild q e Word32
spendScript txIn r = do
    singleton $ Spend txIn (ScriptWitness r)
    singleton $ Peek $ \tx ->
        let ins = tx ^. bodyTxL . inputsTxBodyL
         in if Set.member txIn ins
                then Ok (spendingIndex txIn ins)
                else Iterate 0

-- | Add a reference input.
reference :: TxIn -> TxBuild q e ()
reference = singleton . Reference

-- | Add a collateral input.
collateral :: TxIn -> TxBuild q e ()
collateral txIn = singleton $ Collateral txIn

{- | Pay value to an address. Returns the output
index in the final output list (resolved via
'Peek').
-}
payTo ::
    Addr -> MaryValue -> TxBuild q e Word32
payTo addr val = do
    singleton $ Send $ mkBasicTxOut addr val
    singleton $ Peek $ \tx ->
        let outs = tx ^. bodyTxL . outputsTxBodyL
            target = mkBasicTxOut addr val
         in case elemIndex target (toList outs) of
                Just i -> Ok (fromIntegral i)
                Nothing -> Iterate 0

-- | Add a raw output. Returns the output index.
output ::
    TxOut ConwayEra -> TxBuild q e Word32
output txOut = do
    singleton $ Send txOut
    singleton $ Peek $ \tx ->
        let outs = tx ^. bodyTxL . outputsTxBodyL
         in case elemIndex txOut (toList outs) of
                Just i -> Ok (fromIntegral i)
                Nothing -> Iterate 0

{- | Pay value with a typed inline datum.
Returns the output index.
-}
payTo' ::
    (ToData d) =>
    Addr ->
    MaryValue ->
    d ->
    TxBuild q e Word32
payTo' addr val datum = do
    singleton $
        Send $
            mkBasicTxOut addr val
                & datumTxOutL
                    .~ mkInlineDatum (toPlcData datum)
    singleton $ Peek $ \tx ->
        let outs = tx ^. bodyTxL . outputsTxBodyL
            target =
                mkBasicTxOut addr val
                    & datumTxOutL
                        .~ mkInlineDatum
                            (toPlcData datum)
         in case elemIndex target (toList outs) of
                Just i -> Ok (fromIntegral i)
                Nothing -> Iterate 0

{- | Mint or burn tokens. Positive = mint,
negative = burn. Zero-amount entries are skipped.
-}
mint ::
    (ToData r) =>
    PolicyID ->
    Map AssetName Integer ->
    r ->
    TxBuild q e ()
mint pid assets r =
    mapM_
        ( \(name, qty) ->
            singleton $
                MintI pid name qty (PlutusScriptWitness r)
        )
        [ (n, q)
        | (n, q) <- Map.toList assets
        , q /= 0
        ]

-- | Withdraw stake rewards from a pub-key account.
withdraw :: AccountAddress -> Coin -> TxBuild q e ()
withdraw rewardAccount amount =
    singleton $ Withdraw rewardAccount amount PubKeyWithdraw

-- | Withdraw stake rewards from a script-backed account.
withdrawScript ::
    (ToData r) =>
    AccountAddress ->
    Coin ->
    r ->
    TxBuild q e ()
withdrawScript rewardAccount amount redeemer =
    singleton $
        Withdraw
            rewardAccount
            amount
            (ScriptWithdraw redeemer)

{- | Emit a Conway certificate. Returns the certificate index in the
final transaction body field.
-}
certify ::
    ConwayTxCert ConwayEra ->
    CertWitness ->
    TxBuild q e Word32
certify cert witness = do
    singleton $ Certify cert witness
    singleton $ Peek $ \tx ->
        let certs = tx ^. bodyTxL . certsTxBodyL
         in if cert `elem` toList certs
                then Ok (certIndex cert certs)
                else Iterate 0

{- | Register a stake credential and delegate its vote to the
always-abstain DRep. Returns the final certificate body-field index.
-}
registerAndVoteAbstain ::
    Credential Staking ->
    Coin ->
    CertWitness ->
    TxBuild q e Word32
registerAndVoteAbstain credential deposit =
    certify $
        ConwayTxCertDeleg $
            ConwayRegDelegCert
                credential
                (DelegVote DRepAlwaysAbstain)
                deposit

{- | Emit a Conway proposal procedure. Returns the proposal index in
the final transaction body field.
-}
propose ::
    ProposalProcedure ConwayEra ->
    ProposalWitness ->
    TxBuild q e Word32
propose proposal witness = do
    singleton $ Propose proposal witness
    singleton $ Peek $ \tx ->
        let proposals =
                tx ^. bodyTxL . proposalProceduresTxBodyL
         in if OSet.member proposal proposals
                then Ok (proposalIndex proposal proposals)
                else Iterate 0

{- | Emit a treasury-withdrawal governance action as a proposal
procedure. Returns the final proposal body-field index.
-}
proposeTreasuryWithdrawal ::
    Coin ->
    AccountAddress ->
    Anchor ->
    Map AccountAddress Coin ->
    StrictMaybe ScriptHash ->
    ProposalWitness ->
    TxBuild q e Word32
proposeTreasuryWithdrawal
    deposit
    returnAccount
    anchor
    withdrawals
    guardrail =
        propose $
            ProposalProcedure
                deposit
                returnAccount
                (TreasuryWithdrawals withdrawals guardrail)
                anchor

-- | Vote on a Conway governance action.
vote ::
    Voter ->
    GovActionId ->
    Vote ->
    StrictMaybe Anchor ->
    TxBuild q e ()
vote voter actionId voteChoice anchor =
    singleton $
        VoteI
            voter
            actionId
            (VotingProcedure voteChoice anchor)

-- | Set transaction metadata for a label.
setMetadata :: Word64 -> Metadatum -> TxBuild q e ()
setMetadata label = singleton . SetMetadata label

-- | Set the lower validity bound.
validFrom :: SlotNo -> TxBuild q e ()
validFrom = singleton . SetValidFrom

-- | Set the upper validity bound.
validTo :: SlotNo -> TxBuild q e ()
validTo = singleton . SetValidTo

{- | Override the address that receives the
collateral-return output emitted by 'build' for
script-bearing Conway txs.

Defaults to the @changeAddr@ argument of 'build'
when the program never calls this combinator.
Last-write-wins: calling this multiple times keeps
only the final address, matching 'validFrom' /
'validTo'.

For non-script txs (no redeemers) this combinator
has no effect: 'build' does not emit
@collateral_return@ regardless. See issue #124.
-}
setCollateralReturn :: Addr -> TxBuild q e ()
setCollateralReturn = singleton . SetCollReturn

-- | Require a key signature.
requireSignature ::
    KeyHash Guard -> TxBuild q e ()
requireSignature = singleton . ReqSignature

-- | Attach a Plutus script to the transaction.
attachScript :: Script ConwayEra -> TxBuild q e ()
attachScript = singleton . AttachScript

-- | Peek at the final assembled Tx.
peek ::
    (ConwayTx -> Convergence a) ->
    TxBuild q e a
peek = singleton . Peek

-- | Validate the final converged transaction.
valid ::
    (ConwayTx -> Check e) ->
    TxBuild q e ()
valid = singleton . Valid

-- | Query pluggable build context.
ctx :: q a -> TxBuild q e a
ctx = singleton . Ctx

-- | Check that the indexed output meets the min-UTxO threshold.
checkMinUtxo ::
    PParams ConwayEra ->
    Word32 ->
    TxBuild q e ()
checkMinUtxo pp outIx =
    valid $ \tx ->
        case txOutAt outIx (tx ^. bodyTxL . outputsTxBodyL) of
            Nothing -> Pass
            Just txOut ->
                let actual = txOut ^. coinTxOutL
                    required = getMinCoinTxOut pp txOut
                 in if actual >= required
                        then Pass
                        else
                            LedgerFail $
                                MinUtxoViolation
                                    outIx
                                    actual
                                    required

-- | Check that the CBOR-encoded transaction fits within max size.
checkTxSize :: PParams ConwayEra -> TxBuild q e ()
checkTxSize pp =
    valid $ \tx ->
        let actual =
                fromIntegral $
                    BS.length $
                        serialize' tx
            limit =
                fromIntegral $
                    pp ^. ppMaxTxSizeL
         in if actual <= limit
                then Pass
                else
                    LedgerFail $
                        TxSizeExceeded actual limit

-- ----------------------------------------------------
-- Interpreter state
-- ----------------------------------------------------

-- | Accumulated state from interpreting 'TxBuild'.
data TxState e = TxState
    { tsSpends :: [(TxIn, SpendWitness)]
    , tsRefIns :: [TxIn]
    , tsCollIns :: [TxIn]
    , tsOuts :: [TxOut ConwayEra]
    , tsMints ::
        [ ( PolicyID
          , AssetName
          , Integer
          , MintWitness
          )
        ]
    , tsWithdrawals ::
        [(AccountAddress, Coin, WithdrawWitness)]
    , tsCerts ::
        [(ConwayTxCert ConwayEra, CertWitness)]
    , tsProposals ::
        [(ProposalProcedure ConwayEra, ProposalWitness)]
    , tsVotes ::
        [(Voter, GovActionId, VotingProcedure ConwayEra)]
    , tsMetadata :: Map Word64 Metadatum
    , tsSigners :: Set (KeyHash Guard)
    , tsScripts ::
        Map ScriptHash (Script ConwayEra)
    , tsValidFrom :: StrictMaybe SlotNo
    , tsValidTo :: StrictMaybe SlotNo
    , tsCollReturnAddr :: StrictMaybe Addr
    , tsChecks :: [ConwayTx -> Check e]
    }

emptyState :: TxState e
emptyState =
    TxState
        { tsSpends = []
        , tsRefIns = []
        , tsCollIns = []
        , tsOuts = []
        , tsMints = []
        , tsWithdrawals = []
        , tsCerts = []
        , tsProposals = []
        , tsVotes = []
        , tsMetadata = Map.empty
        , tsSigners = Set.empty
        , tsScripts = Map.empty
        , tsValidFrom = SNothing
        , tsValidTo = SNothing
        , tsCollReturnAddr = SNothing
        , tsChecks = []
        }

{- | Interpret a 'TxBuild' program into 'TxState'.
The 'Tx' argument resolves 'Peek' nodes.
-}
interpretWith ::
    Interpret q ->
    ConwayTx ->
    TxBuild q e a ->
    -- | (state, result, all converged?)
    (TxState e, a, Bool)
interpretWith interpret currentTx prog =
    runIdentity $
        interpretWithM
            (pure . runInterpret interpret)
            currentTx
            prog

interpretWithM ::
    (Monad m) =>
    (forall x. q x -> m x) ->
    ConwayTx ->
    TxBuild q e a ->
    m (TxState e, a, Bool)
interpretWithM runCtx currentTx = go emptyState True
  where
    go st conv prog = case view prog of
        Return a -> pure (st, a, conv)
        Spend txIn w :>>= k ->
            go
                st
                    { tsSpends =
                        tsSpends st ++ [(txIn, w)]
                    }
                conv
                (k ())
        Reference txIn :>>= k ->
            go
                st
                    { tsRefIns =
                        tsRefIns st ++ [txIn]
                    }
                conv
                (k ())
        Collateral txIn :>>= k ->
            go
                st
                    { tsCollIns =
                        tsCollIns st ++ [txIn]
                    }
                conv
                (k ())
        Send txOut :>>= k ->
            go
                st
                    { tsOuts =
                        tsOuts st ++ [txOut]
                    }
                conv
                (k ())
        MintI pid name qty w :>>= k ->
            go
                st
                    { tsMints =
                        tsMints st
                            ++ [(pid, name, qty, w)]
                    }
                conv
                (k ())
        Withdraw rewardAccount amount w :>>= k ->
            go
                st
                    { tsWithdrawals =
                        tsWithdrawals st
                            ++ [(rewardAccount, amount, w)]
                    }
                conv
                (k ())
        Certify cert w :>>= k ->
            go
                st
                    { tsCerts =
                        tsCerts st ++ [(cert, w)]
                    }
                conv
                (k ())
        Propose proposal w :>>= k ->
            go
                st
                    { tsProposals =
                        tsProposals st ++ [(proposal, w)]
                    }
                conv
                (k ())
        VoteI voter actionId procedure :>>= k ->
            go
                st
                    { tsVotes =
                        tsVotes st
                            ++ [(voter, actionId, procedure)]
                    }
                conv
                (k ())
        SetMetadata label metadatum :>>= k ->
            go
                st
                    { tsMetadata =
                        Map.insert
                            label
                            metadatum
                            (tsMetadata st)
                    }
                conv
                (k ())
        ReqSignature kh :>>= k ->
            go
                st
                    { tsSigners =
                        Set.insert kh (tsSigners st)
                    }
                conv
                (k ())
        AttachScript script :>>= k ->
            go
                st
                    { tsScripts =
                        Map.insert
                            (hashScript script)
                            script
                            (tsScripts st)
                    }
                conv
                (k ())
        SetValidFrom slot :>>= k ->
            go
                st
                    { tsValidFrom = SJust slot
                    }
                conv
                (k ())
        SetValidTo slot :>>= k ->
            go
                st
                    { tsValidTo = SJust slot
                    }
                conv
                (k ())
        SetCollReturn addr :>>= k ->
            go
                st
                    { tsCollReturnAddr = SJust addr
                    }
                conv
                (k ())
        Peek f :>>= k ->
            case f currentTx of
                Ok a -> go st conv (k a)
                Iterate a ->
                    go st False (k a)
        Valid chk :>>= k ->
            go
                st
                    { tsChecks =
                        tsChecks st ++ [chk]
                    }
                conv
                (k ())
        Ctx q :>>= k -> do
            a <- runCtx q
            go st conv (k a)

-- | Assemble a 'Tx' from interpreter state.
assembleTx :: PParams ConwayEra -> TxState e -> ConwayTx
assembleTx = assembleTxWith Set.empty

{- | Assemble with extra input TxIns (e.g. fee UTxO).
These are included in the input set for correct
spending index computation but don't get redeemers.
-}
assembleTxWith ::
    Set.Set TxIn -> PParams ConwayEra -> TxState e -> ConwayTx
assembleTxWith extraIns pp st =
    let
        allSpendIns =
            Set.union extraIns $
                Set.fromList $
                    map fst (tsSpends st)
        refIns = Set.fromList (tsRefIns st)
        collIns = Set.fromList (tsCollIns st)
        outs = StrictSeq.fromList (tsOuts st)
        mintMA = foldl' addMint mempty (tsMints st)
        withdrawalEntries =
            collectWithdrawalEntries
                (tsWithdrawals st)
        withdrawals =
            Withdrawals
                (Map.map fst withdrawalEntries)
        certs =
            StrictSeq.fromList $
                map fst (tsCerts st)
        proposals =
            OSet.fromList $
                map fst (tsProposals st)
        votes =
            VotingProcedures $
                Map.fromListWith
                    Map.union
                    [ ( voter
                      , Map.singleton actionId procedure
                      )
                    | (voter, actionId, procedure) <- tsVotes st
                    ]
        -- Build redeemers
        spendRdmrs =
            collectSpendRedeemers
                allSpendIns
                (tsSpends st)
        mintRdmrs = collectMintRedeemers (tsMints st)
        withdrawalRdmrs =
            collectWithdrawalRedeemers
                withdrawals
                withdrawalEntries
        certRdmrs =
            collectCertRedeemers
                certs
                (tsCerts st)
        proposalRdmrs =
            collectProposalRedeemers
                proposals
                (tsProposals st)
        rdmrList =
            spendRdmrs
                ++ mintRdmrs
                ++ withdrawalRdmrs
                ++ certRdmrs
                ++ proposalRdmrs
        allRdmrs = Redeemers $ Map.fromList rdmrList
        auxData =
            if Map.null (tsMetadata st)
                then SNothing
                else
                    SJust $
                        mkBasicTxAuxData
                            & metadataTxAuxDataL
                                .~ tsMetadata st
        -- Integrity hash (skip if no scripts)
        integrity =
            if null rdmrList
                then SNothing
                else
                    computeScriptIntegrity
                        PlutusV3
                        pp
                        allRdmrs
        body =
            mkBasicTxBody
                & inputsTxBodyL .~ allSpendIns
                & outputsTxBodyL .~ outs
                & referenceInputsTxBodyL .~ refIns
                & collateralInputsTxBodyL .~ collIns
                & mintTxBodyL .~ mintMA
                & withdrawalsTxBodyL .~ withdrawals
                & certsTxBodyL .~ certs
                & proposalProceduresTxBodyL .~ proposals
                & votingProceduresTxBodyL .~ votes
                & reqSignerHashesTxBodyL
                    .~ tsSigners st
                & vldtTxBodyL
                    .~ ValidityInterval
                        { invalidBefore = tsValidFrom st
                        , invalidHereafter = tsValidTo st
                        }
                & auxDataHashTxBodyL
                    .~ fmap hashTxAuxData auxData
                & scriptIntegrityHashTxBodyL
                    .~ integrity
     in
        mkBasicTx body
            & witsTxL . scriptTxWitsL
                .~ tsScripts st
            & witsTxL . rdmrsTxWitsL
                .~ allRdmrs
            & auxDataTxL
                .~ auxData

-- ----------------------------------------------------
-- Assembly
-- ----------------------------------------------------

{- | Assemble a 'TxBuild' program into a 'Tx'
without evaluation or balancing.

Runs one pass: interprets with the initial (empty)
Tx, then assembles from collected steps. 'Peek'
nodes see the draft Tx on a second internal pass.
-}
draft ::
    PParams ConwayEra ->
    TxBuild q e a ->
    ConwayTx
draft pp = draftWith pp noCtxInterpret

noCtxInterpret :: Interpret q
noCtxInterpret =
    Interpret $
        const $
            error
                "draft: encountered ctx without draftWith interpreter"

draftWith ::
    PParams ConwayEra ->
    Interpret q ->
    TxBuild q e a ->
    ConwayTx
draftWith pp interpret prog =
    let
        -- Pass 1: collect steps with bogus Tx
        initialTx = mkBasicTx mkBasicTxBody
        (st1, _, _) =
            interpretWith interpret initialTx prog
        -- Assemble from pass 1
        tx1 = assembleTx pp st1
        -- Pass 2: re-interpret with real Tx for
        -- Peek resolution
        (st2, _, _) =
            interpretWith interpret tx1 prog
     in
        assembleTx pp st2

-- | Errors from 'build'.
data BuildError e
    = -- | Script evaluation failure.
      EvalFailure
        (ConwayPlutusPurpose AsIx ConwayEra)
        String
    | -- | Balance failure.
      BalanceFailed BalanceError
    | -- | Validation failures on the final Tx.
      ChecksFailed [Check e]
    | -- | Internal error adjusting the fee.
      BumpFeeFailed String
    deriving (Show)

{- | Assemble, evaluate scripts, and balance.

Iterates until all 'Peek' nodes return 'Ok' and
the Tx body stabilizes:

1. Interpret program with current Tx (resolve Peek)
2. Assemble Tx from steps
3. Add input UTxOs, evaluate scripts with inflated
   budgets → ExUnits
4. Patch redeemers, recompute integrity hash, and
   balance (fee + change)
5. Re-evaluate the balanced body, patch the
   original unbalanced body with those ExUnits, and
   balance again
6. If any Peek returned Iterate, fee changed, or
   ExUnits changed after balancing → 1
-}

{- | Knobs that influence 'buildWith'. New options
will be added here without breaking 'build', whose
default behaviour is preserved by 'defaultBuildOptions'.
-}
data BuildOptions = BuildOptions
    { boExUnitsMargin :: ExUnits -> ExUnits
    -- ^ Transform each redeemer's evaluated 'ExUnits'
    -- before the integrity hash is recomputed and the
    -- tx is balanced. Defaults to 'id'.
    --
    -- Use a small overshoot (e.g. @1.20×@, mirroring
    -- @cardano-cli@) when the client-side ledger
    -- evaluator (the one running here, via
    -- @evalTxExUnits@) is older than the cluster
    -- node's: the same script can cost a few hundred
    -- mem / a few hundred-thousand steps more on the
    -- newer side, and that delta lands as
    -- 'PlutusFailure' at submit time even though the
    -- shape of the tx is correct.
    , boCollateralUtxos :: CollateralUtxos
    -- ^ Resolution map for the body's collateral
    -- inputs (issue #124). Used to compute the
    -- @total_collateral@ / @collateral_return@
    -- arithmetic during balancing. These UTxOs are
    -- NOT added to the body's @inputs@ — they only
    -- contribute lovelace to the collateral fields.
    --
    -- Pass @CollateralUtxos []@ (the default) when
    -- the program uses the same UTxO for both a
    -- regular @spend@ and a @collateral@ instruction,
    -- since 'inputUtxos' already covers both lookups
    -- in that case. Pass the collateral-only UTxOs
    -- here when the collateral inputs are different
    -- from the regular spend inputs.
    --
    -- This field is input data rather than a tunable
    -- knob; it lives here for backwards compatibility
    -- with the @build@ / @buildWith@ signatures.
    }

-- | All defaults preserve pre-'buildWith' behaviour.
defaultBuildOptions :: BuildOptions
defaultBuildOptions =
    BuildOptions
        { boExUnitsMargin = id
        , boCollateralUtxos = CollateralUtxos []
        }

build ::
    PParams ConwayEra ->
    InterpretIO q ->
    -- | Script evaluator
    ( ConwayTx ->
      IO
        ( Map
            ( ConwayPlutusPurpose
                AsIx
                ConwayEra
            )
            (Either String ExUnits)
        )
    ) ->
    -- | All input UTxOs
    [(TxIn, TxOut ConwayEra)] ->
    -- | Resolved reference-input UTxOs. Their
    --     'referenceScriptTxOutL' bytes feed
    --     'estimateMinFeeTx' so Conway's
    --     @minFeeRefScriptCostPerByte@ is charged
    --     correctly. Pass @[]@ if the tx has no
    --     ref-input scripts.
    [(TxIn, TxOut ConwayEra)] ->
    -- | Change address
    Addr ->
    TxBuild q e a ->
    IO (Either (BuildError e) ConwayTx)
build = buildWith defaultBuildOptions

-- | 'build' with explicit 'BuildOptions'.
buildWith ::
    BuildOptions ->
    PParams ConwayEra ->
    InterpretIO q ->
    ( ConwayTx ->
      IO
        ( Map
            ( ConwayPlutusPurpose
                AsIx
                ConwayEra
            )
            (Either String ExUnits)
        )
    ) ->
    [(TxIn, TxOut ConwayEra)] ->
    [(TxIn, TxOut ConwayEra)] ->
    Addr ->
    TxBuild q e a ->
    IO (Either (BuildError e) ConwayTx)
buildWith opts pp interpret evaluateTx inputUtxos refUtxos changeAddr prog =
    step Set.empty (Coin 0) 0 (mkBasicTx mkBasicTxBody)
  where
    -- Pre-compute the extra TxIns from inputUtxos
    -- so Peek-based index computation sees ALL
    -- inputs (including fee UTxO).
    extraIns =
        Set.fromList $ map fst inputUtxos
    addExtras tx =
        let existing =
                tx ^. bodyTxL . inputsTxBodyL
         in tx
                & bodyTxL . inputsTxBodyL
                    .~ Set.union existing extraIns

    -- \| Total bytes of any reference scripts
    -- attached to UTxOs in @refUtxos@ that the
    -- current tx body actually references. Conway
    -- charges these via @minFeeRefScriptCostPerByte@.
    refScriptBytesOf tx =
        refScriptsSize
            (tx ^. bodyTxL . referenceInputsTxBodyL)
            refUtxos

    inflateRedeemerBudgets tx =
        let Redeemers rdmrs =
                tx ^. witsTxL . rdmrsTxWitsL
            inflated =
                Redeemers $
                    fmap
                        ( \(d, _) ->
                            (d, evalBudgetExUnits)
                        )
                        rdmrs
            integrity =
                if Map.null rdmrs
                    then SNothing
                    else
                        computeScriptIntegrity
                            PlutusV3
                            pp
                            inflated
         in tx
                & witsTxL . rdmrsTxWitsL
                    .~ inflated
                & bodyTxL
                    . scriptIntegrityHashTxBodyL
                    .~ integrity

    evaluateForBudget tx =
        evaluateTx (inflateRedeemerBudgets tx)

    evalFailures evalResult =
        [ (p, e)
        | (p, Left e) <-
            Map.toList evalResult
        ]

    balanceWithEval st tx evalResult =
        balanceTxWith
            pp
            inputUtxos
            (boCollateralUtxos opts)
            refUtxos
            changeAddr
            (strictMaybeToMaybe (tsCollReturnAddr st))
            (patchExUnits tx evalResult)

    -- \| One iteration: interpret, assemble, eval,
    -- patch, balance. Track seen fees to detect
    -- oscillation and bisect.
    step seenFees maxFee evalRetries prevTx = do
        -- 1. Interpret
        let prevWithIns = addExtras prevTx
        (st, _, peekConverged) <-
            interpretWithM
                (runInterpretIO interpret)
                prevWithIns
                prog
        let tx = assembleTxWith extraIns pp st
            prevFee = prevTx ^. bodyTxL . feeTxBodyL
            txForEval =
                tx & bodyTxL . feeTxBodyL .~ prevFee
        -- 2. Eval (no change output; scripts that
        --    check conservation use tx.fee which
        --    matches Peek-computed outputs).
        --    Inflate redeemer ExUnits to max budget
        --    so the evaluator gives scripts enough
        --    room to execute. The real ExUnits come
        --    back in evalResult.
        evalResult <- evaluateForBudget txForEval
        let failures = evalFailures evalResult
        case failures of
            ((purpose, msg) : _) -> do
                -- Eval failed. Distinguish terminal
                -- script failures from retryable
                -- fee-search failures.
                let estFee =
                        estimateMinFeeTx
                            pp
                            txForEval
                            1
                            0
                            (refScriptBytesOf txForEval)
                if evalRetries >= (1 :: Int)
                    then
                        -- Already retried once with a
                        -- fee estimate. The failure is
                        -- stable — surface it.
                        pure $
                            Left $
                                EvalFailure purpose msg
                    else
                        if prevFee > Coin 0
                            then do
                                -- A prior iteration
                                -- succeeded. Reuse its
                                -- ExUnits to avoid a
                                -- retry loop.
                                let prevEUs =
                                        Map.map Right $
                                            fmap snd $
                                                let Redeemers r =
                                                        prevTx
                                                            ^. witsTxL
                                                                . rdmrsTxWitsL
                                                 in r
                                    patchedTx =
                                        patchExUnits
                                            tx
                                            prevEUs
                                case balanceTxWith
                                    pp
                                    inputUtxos
                                    (boCollateralUtxos opts)
                                    refUtxos
                                    changeAddr
                                    (strictMaybeToMaybe (tsCollReturnAddr st))
                                    patchedTx of
                                    Left err ->
                                        pure $
                                            Left $
                                                BalanceFailed
                                                    err
                                    Right BalanceResult{balancedTx = balanced} -> do
                                        let finalFee =
                                                balanced
                                                    ^. bodyTxL
                                                        . feeTxBodyL
                                        if finalFee
                                            == prevFee
                                            then
                                                pure $
                                                    Right
                                                        balanced
                                            else
                                                step
                                                    ( Set.insert
                                                        finalFee
                                                        seenFees
                                                    )
                                                    ( max
                                                        maxFee
                                                        finalFee
                                                    )
                                                    0
                                                    balanced
                            else do
                                -- First iteration
                                -- (prevFee=0). Retry
                                -- with estimated fee.
                                let retryTx =
                                        tx
                                            & bodyTxL
                                                . feeTxBodyL
                                                .~ estFee
                                step
                                    seenFees
                                    maxFee
                                    (evalRetries + 1)
                                    retryTx
            [] -> do
                -- 3. Balance once from the
                --    pre-balance evaluation, then
                --    evaluate the balanced body that
                --    validators will actually see.
                case balanceWithEval st tx evalResult of
                    Left err ->
                        pure $
                            Left $
                                BalanceFailed err
                    Right BalanceResult{balancedTx = balanced0} -> do
                        balancedEvalResult <-
                            evaluateForBudget balanced0
                        case evalFailures balancedEvalResult of
                            ((purpose, msg) : _) ->
                                -- Balanced eval can legitimately
                                -- fail when scripts read fee via
                                -- 'Peek': the pre-balance eval
                                -- saw fee=prevFee but the
                                -- post-balance body has a fresh
                                -- fee that violates whatever
                                -- conservation the script was
                                -- enforcing. The next interpret
                                -- will re-read 'Peek' against
                                -- the balanced body and the
                                -- script will see the new fee,
                                -- so iterate with 'balanced0' as
                                -- the new prevTx. Fall through
                                -- to a terminal error only when
                                -- we've already seen the same
                                -- fee before (no progress) or
                                -- after one retry past prevFee>0
                                -- — same shape as the
                                -- pre-balance eval-failure path.
                                let finalFee =
                                        balanced0
                                            ^. bodyTxL
                                                . feeTxBodyL
                                 in if Set.member finalFee seenFees
                                        || evalRetries
                                            >= (1 :: Int)
                                        then
                                            pure $
                                                Left $
                                                    EvalFailure
                                                        purpose
                                                        msg
                                        else
                                            step
                                                ( Set.insert
                                                    finalFee
                                                    seenFees
                                                )
                                                (max maxFee finalFee)
                                                (evalRetries + 1)
                                                balanced0
                            [] ->
                                continueAfterBalancedEval
                                    st
                                    peekConverged
                                    prevFee
                                    seenFees
                                    maxFee
                                    tx
                                    balancedEvalResult

    continueAfterBalancedEval
        st
        peekConverged
        prevFee
        seenFees
        maxFee
        tx
        balancedEvalResult =
            -- Patch the original unbalanced body with
            -- the balanced-body ExUnits, then balance
            -- again so fee and change account for
            -- those final costs without appending a
            -- second change output.
            case balanceWithEval st tx balancedEvalResult of
                Left err ->
                    pure $
                        Left $
                            BalanceFailed err
                Right BalanceResult{balancedTx = balanced, changeIndex = chIx} -> do
                    let finalFee =
                            balanced
                                ^. bodyTxL
                                    . feeTxBodyL
                        newMax =
                            max maxFee finalFee
                    if finalFee == prevFee
                        then
                            if newMax > finalFee
                                then
                                    -- Fee converged
                                    -- but below max.
                                    -- Re-iterate
                                    -- once with max
                                    -- so Peek sees
                                    -- the right fee.
                                    case bumpFee
                                        chIx
                                        balanced
                                        newMax of
                                        Left msg ->
                                            pure $
                                                Left $
                                                    BumpFeeFailed
                                                        msg
                                        Right bumped ->
                                            step
                                                seenFees
                                                newMax
                                                0
                                                bumped
                                else
                                    if not peekConverged
                                        && finalFee
                                            > Coin 0
                                        then
                                            -- Fee converged
                                            -- but Peek has
                                            -- not. Re-iterate.
                                            step
                                                seenFees
                                                newMax
                                                0
                                                balanced
                                        else
                                            -- Truly
                                            -- converged.
                                            case failedChecks
                                                (tsChecks st)
                                                balanced of
                                                [] ->
                                                    pure $
                                                        Right
                                                            balanced
                                                errs ->
                                                    pure $
                                                        Left $
                                                            ChecksFailed
                                                                errs
                        else
                            if Set.member
                                finalFee
                                seenFees
                                then do
                                    -- Oscillation!
                                    let lo =
                                            min
                                                finalFee
                                                prevFee
                                        hi =
                                            max
                                                finalFee
                                                prevFee
                                    bisect
                                        st
                                        balancedEvalResult
                                        chIx
                                        balanced
                                        lo
                                        hi
                                else
                                    step
                                        ( Set.insert
                                            finalFee
                                            seenFees
                                        )
                                        newMax
                                        0
                                        balanced

    -- \| Binary search for the smallest fee where
    -- eval passes. lo fails eval, hi passes.
    bisect st evalResult chIx templateTx lo hi
        | unCoin hi <= unCoin lo + 1 =
            -- hi is the smallest valid fee.
            -- Build final tx with hi.
            finalize st evalResult chIx templateTx hi
        | otherwise = do
            let mid =
                    Coin $
                        unCoin lo
                            + (unCoin hi - unCoin lo)
                                `div` 2
            -- Re-interpret with mid fee
            case bumpFee chIx templateTx mid of
                Left msg ->
                    pure $ Left $ BumpFeeFailed msg
                Right midTx -> do
                    let midWithIns = addExtras midTx
                    (st', _, _) <-
                        interpretWithM
                            (runInterpretIO interpret)
                            midWithIns
                            prog
                    let tx' =
                            assembleTxWith
                                extraIns
                                pp
                                st'
                                & bodyTxL . feeTxBodyL
                                    .~ mid
                    -- Balance to get change output
                    case balanceTxWith
                        pp
                        inputUtxos
                        (boCollateralUtxos opts)
                        refUtxos
                        changeAddr
                        (strictMaybeToMaybe (tsCollReturnAddr st'))
                        tx' of
                        Left _ ->
                            -- Can't balance at mid
                            bisect
                                st
                                evalResult
                                chIx
                                templateTx
                                mid
                                hi
                        Right BalanceResult{balancedTx = balanced, changeIndex = chIx'} ->
                            case bumpFee chIx' balanced mid of
                                Left msg ->
                                    pure $
                                        Left $
                                            BumpFeeFailed
                                                msg
                                Right midBal -> do
                                    evalResult' <-
                                        evaluateTx
                                            midBal
                                    let failures' =
                                            [ e
                                            | ( _
                                                , Left e
                                                ) <-
                                                Map.toList
                                                    evalResult'
                                            ]
                                    if null failures'
                                        then
                                            bisect
                                                st'
                                                evalResult'
                                                chIx'
                                                midBal
                                                lo
                                                mid
                                        else
                                            bisect
                                                st
                                                evalResult
                                                chIx
                                                templateTx
                                                mid
                                                hi

    -- \| Finalize with a specific fee.
    --
    -- Re-interpret + assemble so Peek sees the
    -- chosen fee, patch ExUnits, then balance.
    -- If balanceTx lowered the fee (it computes
    -- min_fee), bump it back and shrink the change
    -- output to compensate.
    finalize _st evalResult _chIx _templateTx fee = do
        -- Re-interpret with the chosen fee
        let feeTx =
                mkBasicTx mkBasicTxBody
                    & bodyTxL . feeTxBodyL .~ fee
            feeTxWithIns = addExtras feeTx
        (st', _, _) <-
            interpretWithM
                (runInterpretIO interpret)
                feeTxWithIns
                prog
        let tx' =
                assembleTxWith extraIns pp st'
                    & bodyTxL . feeTxBodyL .~ fee
            patched =
                patchExUnits tx' evalResult
        case balanceTxWith
            pp
            inputUtxos
            (boCollateralUtxos opts)
            refUtxos
            changeAddr
            (strictMaybeToMaybe (tsCollReturnAddr st'))
            patched of
            Left err ->
                pure $ Left $ BalanceFailed err
            Right BalanceResult{balancedTx = balanced, changeIndex = chIx} -> do
                let balFee =
                        balanced
                            ^. bodyTxL . feeTxBodyL
                    eFinal =
                        if balFee == fee
                            then Right balanced
                            else bumpFee chIx balanced fee
                case eFinal of
                    Left msg ->
                        pure $
                            Left $
                                BumpFeeFailed msg
                    Right final ->
                        case failedChecks
                            (tsChecks st')
                            final of
                            [] ->
                                pure $ Right final
                            errs ->
                                pure $
                                    Left $
                                        ChecksFailed
                                            errs

    -- \| Patch ExUnits from eval result, applying
    -- the caller-supplied 'boExUnitsMargin'
    -- transform. Default options leave the value
    -- untouched (identity).
    patchExUnits tx evalResult =
        let Redeemers rdmrMap =
                tx ^. witsTxL . rdmrsTxWitsL
            margin = boExUnitsMargin opts
            patched =
                Map.mapWithKey
                    ( \purpose (dat, eu) ->
                        case Map.lookup
                            purpose
                            evalResult of
                            Just (Right eu') ->
                                (dat, margin eu')
                            _ -> (dat, eu)
                    )
                    rdmrMap
            newRdmrs = Redeemers patched
            integrity =
                if Map.null patched
                    then SNothing
                    else
                        computeScriptIntegrity
                            PlutusV3
                            pp
                            newRdmrs
         in tx
                & witsTxL . rdmrsTxWitsL
                    .~ newRdmrs
                & bodyTxL
                    . scriptIntegrityHashTxBodyL
                    .~ integrity

{- | Bump fee from what balanceTx set to a higher
target, reducing the change output to compensate.

When bisection finds a fee > min_fee,
balanceTx sets fee = min_fee and puts the
excess into the change output. This function
moves the difference back: increase fee,
decrease change.

The change output is identified by index
(from 'BalanceResult'), not by position.
-}
bumpFee ::
    -- | Change output index
    Int ->
    ConwayTx ->
    Coin ->
    Either String ConwayTx
bumpFee chIdx tx targetFee =
    let currentFee = tx ^. bodyTxL . feeTxBodyL
        diff = unCoin targetFee - unCoin currentFee
        outs =
            toList
                (tx ^. bodyTxL . outputsTxBodyL)
     in if chIdx < 0 || chIdx >= length outs
            then
                Left
                    "bumpFee: change index \
                    \out of range"
            else case splitAt chIdx outs of
                (_, []) ->
                    Left
                        "bumpFee: change index \
                        \out of range"
                (before, changeOut : after) ->
                    let Coin changeVal =
                            changeOut ^. coinTxOutL
                        adjusted =
                            changeOut
                                & coinTxOutL
                                    .~ Coin
                                        (changeVal - diff)
                        newOuts =
                            StrictSeq.fromList
                                ( before
                                    ++ [adjusted]
                                    ++ after
                                )
                     in Right $
                            tx
                                & bodyTxL . feeTxBodyL
                                    .~ targetFee
                                & bodyTxL
                                    . outputsTxBodyL
                                    .~ newOuts

-- ----------------------------------------------------
-- Internal helpers
-- ----------------------------------------------------

withdrawalIndex ::
    AccountAddress ->
    Withdrawals ->
    Word32
withdrawalIndex needle (Withdrawals withdrawals) =
    go 0 (Map.keys withdrawals)
  where
    go _ [] =
        error
            "withdrawalIndex: RewardAccount not in map"
    go n (x : xs)
        | x == needle = n
        | otherwise = go (n + 1) xs

certIndex ::
    ConwayTxCert ConwayEra ->
    StrictSeq.StrictSeq (ConwayTxCert ConwayEra) ->
    Word32
certIndex needle certs =
    go 0 (toList certs)
  where
    go _ [] =
        error "certIndex: certificate not in body"
    go n (x : xs)
        | x == needle = n
        | otherwise = go (n + 1) xs

proposalIndex ::
    ProposalProcedure ConwayEra ->
    OSet.OSet (ProposalProcedure ConwayEra) ->
    Word32
proposalIndex needle proposals =
    go 0 (toList (OSet.toStrictSeq proposals))
  where
    go _ [] =
        error "proposalIndex: proposal not in body"
    go n (x : xs)
        | x == needle = n
        | otherwise = go (n + 1) xs

-- | Collect spending redeemers from steps.
collectSpendRedeemers ::
    Set TxIn ->
    [(TxIn, SpendWitness)] ->
    [ ( ConwayPlutusPurpose AsIx ConwayEra
      , (Data ConwayEra, ExUnits)
      )
    ]
collectSpendRedeemers allIns spends =
    [ ( ConwaySpending (AsIx ix)
      , (toLedgerData r, ExUnits 0 0)
      )
    | (txIn, ScriptWitness r) <- spends
    , let ix = spendingIndex txIn allIns
    ]

-- | Collect minting redeemers. First per policy.
collectMintRedeemers ::
    [(PolicyID, AssetName, Integer, MintWitness)] ->
    [ ( ConwayPlutusPurpose AsIx ConwayEra
      , (Data ConwayEra, ExUnits)
      )
    ]
collectMintRedeemers mints =
    let
        allPolicies =
            Set.fromList
                [pid | (pid, _, _, _) <- mints]
        policyIdx pid =
            go 0 (Set.toAscList allPolicies)
          where
            go _ [] = error "policyIdx: not found"
            go n (x : xs)
                | x == pid = n
                | otherwise = go (n + 1) xs
        seenData =
            foldl' addP Map.empty mints
        addP acc (pid, _, _, PlutusScriptWitness r)
            | Map.member pid acc = acc
            | otherwise =
                Map.insert pid (toLedgerData r) acc
     in
        [ ( ConwayMinting (AsIx (policyIdx pid))
          , (d, ExUnits 0 0)
          )
        | (pid, d) <- Map.toList seenData
        ]

collectWithdrawalEntries ::
    [(AccountAddress, Coin, WithdrawWitness)] ->
    Map AccountAddress (Coin, Maybe (Data ConwayEra))
collectWithdrawalEntries =
    Map.fromList . fmap toEntry
  where
    toEntry (rewardAccount, amount, witness) =
        ( rewardAccount
        , (amount, withdrawWitnessData witness)
        )

collectWithdrawalRedeemers ::
    Withdrawals ->
    Map AccountAddress (Coin, Maybe (Data ConwayEra)) ->
    [ ( ConwayPlutusPurpose AsIx ConwayEra
      , (Data ConwayEra, ExUnits)
      )
    ]
collectWithdrawalRedeemers withdrawals entries =
    [ ( ConwayRewarding
            (AsIx (withdrawalIndex rewardAccount withdrawals))
      , (redeemer, ExUnits 0 0)
      )
    | (rewardAccount, (_, Just redeemer)) <-
        Map.toList entries
    ]

collectCertRedeemers ::
    StrictSeq.StrictSeq (ConwayTxCert ConwayEra) ->
    [(ConwayTxCert ConwayEra, CertWitness)] ->
    [ ( ConwayPlutusPurpose AsIx ConwayEra
      , (Data ConwayEra, ExUnits)
      )
    ]
collectCertRedeemers certs =
    Map.toList . foldl' addScriptCert Map.empty
  where
    finalCerts = toList certs

    addScriptCert acc (cert, ScriptCert redeemer) =
        case elemIndex cert finalCerts of
            Nothing -> acc
            Just certIx ->
                Map.insertWith
                    keepExisting
                    (ConwayCertifying (AsIx (fromIntegral certIx)))
                    (toLedgerData redeemer, ExUnits 0 0)
                    acc
    addScriptCert acc _ = acc

    keepExisting _ existing = existing

collectProposalRedeemers ::
    OSet.OSet (ProposalProcedure ConwayEra) ->
    [(ProposalProcedure ConwayEra, ProposalWitness)] ->
    [ ( ConwayPlutusPurpose AsIx ConwayEra
      , (Data ConwayEra, ExUnits)
      )
    ]
collectProposalRedeemers proposals =
    Map.toList . foldl' addGuardrailProposal Map.empty
  where
    finalProposals = toList (OSet.toStrictSeq proposals)

    addGuardrailProposal acc (proposal, GuardrailProposal redeemer)
        | not (proposalHasGuardrail proposal) = acc
        | otherwise =
            case elemIndex proposal finalProposals of
                Nothing -> acc
                Just proposalIx ->
                    Map.insertWith
                        keepExisting
                        ( ConwayProposing
                            (AsIx (fromIntegral proposalIx))
                        )
                        (toLedgerData redeemer, ExUnits 0 0)
                        acc
    addGuardrailProposal acc _ = acc

    keepExisting _ existing = existing

proposalHasGuardrail ::
    ProposalProcedure ConwayEra -> Bool
proposalHasGuardrail (ProposalProcedure _ _ action _) =
    case action of
        ParameterChange _ _ (SJust _) -> True
        TreasuryWithdrawals _ (SJust _) -> True
        _ -> False

-- | Accumulate 'MultiAsset' from mint entries.
addMint ::
    MultiAsset ->
    (PolicyID, AssetName, Integer, MintWitness) ->
    MultiAsset
addMint acc (pid, name, qty, _) =
    acc
        <> MultiAsset
            ( Map.singleton
                pid
                (Map.singleton name qty)
            )

withdrawWitnessData ::
    WithdrawWitness ->
    Maybe (Data ConwayEra)
withdrawWitnessData PubKeyWithdraw = Nothing
withdrawWitnessData (ScriptWithdraw redeemer) =
    Just (toLedgerData redeemer)

-- | Convert a 'ToData' value to ledger 'Data'.
toLedgerData :: (ToData a) => a -> Data ConwayEra
toLedgerData x =
    let BuiltinData d = toBuiltinData x
     in Data d

-- | Convert a 'ToData' value to 'PlutusCore.Data'.
toPlcData :: (ToData a) => a -> PLC.Data
toPlcData x =
    let BuiltinData d = toBuiltinData x in d

-- | Wrap 'PlutusCore.Data' as an inline 'Datum'.
mkInlineDatum :: PLC.Data -> Datum ConwayEra
mkInlineDatum d =
    Datum $
        dataToBinaryData
            (Data d :: Data ConwayEra)

failedChecks ::
    [ConwayTx -> Check e] ->
    ConwayTx ->
    [Check e]
failedChecks checks tx =
    [ result
    | check <- checks
    , let result = check tx
    , case result of
        Pass -> False
        _ -> True
    ]

txOutAt ::
    Word32 ->
    StrictSeq.StrictSeq (TxOut ConwayEra) ->
    Maybe (TxOut ConwayEra)
txOutAt ix =
    go (fromIntegral ix :: Int) . foldr (:) []
  where
    go _ [] = Nothing
    go 0 (txOut : _) = Just txOut
    go n (_ : rest) = go (n - 1) rest
